pipeline {
    agent any
    
    environment {
        DOCKER_IMAGE_BASE = 'adriandez/cypress-automation'
        CONFIGMAP_PATH = 'build/k8s/cypress-configmap.yaml'
        CONFIGMAP_PATH_CRON = 'build/k8s/cypress-cron-configmap.yaml'
        JOB_TEMPLATE_PATH = 'build/k8s/cypress-job.yaml'
        CRON_JOB_PATH = 'build/k8s/cypress-cron-job.yaml' 
        DOCKERHUB_CREDENTIALS = 'dockerhub-credentials'
        KUBERNETES_CREDENTIALS = 'docker-desktop-kubeconfig'
    }

    parameters {
        choice(
            choices: ['chrome', 'edge', 'firefox'],
            name: 'BROWSER',
            description: 'Browser to use'
        )
        string(
            name: 'BRANCH',
            defaultValue: 'develop',
            description: 'Branch to build'
        )
        booleanParam(
            name: 'LAUNCH_MANUAL',
            defaultValue: false,
            description: 'Launch k8s job manually'
        )
    }

    stages {
        stage('Checkout Code') {
            steps {
                checkout([
                    $class: 'GitSCM',
                    branches: [[name: "${params.BRANCH}"]],
                    userRemoteConfigs: [[
                        credentialsId: 'win-adri-id',
                        url: 'https://github.com/adriandez/cypress-automation.git'
                    ]]
                ])
            }
        }
        
        stage("Build and Push Docker Images") {
            steps {
                script {
                    try {
                        docker.withRegistry('https://index.docker.io/v1/', DOCKERHUB_CREDENTIALS) {
                            ['chrome', 'edge', 'firefox'].each { browser ->
                                def browserImage = docker.build("${DOCKER_IMAGE_BASE}:${browser}", 
                                    "--build-arg INSTALL_BROWSER=${browser} -f build/Dockerfile .")
                                bat "docker inspect ${DOCKER_IMAGE_BASE}:${browser} | findstr INSTALL_BROWSER"
                                browserImage.push()
                            }
                        }
                    } catch (e) {
                        error "Failed to build and push Docker images: ${e}"
                    }
                }
            }
        }

        stage('Clean Up Docker Images') {
            steps {
                script {
                    bat '''
                        for /f "tokens=3" %%i in ('docker images ^| findstr "cypress-automation"') do docker rmi %%i --force
                    '''
                }
            }
        }

        stage('Deploy to Kubernetes Job') {
            when {
                expression { return params.LAUNCH_MANUAL }
            }
            steps {
                script {
                    def jobYaml = readFile(JOB_TEMPLATE_PATH)
                    jobYaml = jobYaml.replace("{{BROWSER}}", params.BROWSER)
                    
                    def tempYamlPath = "build/k8s/cypress-job-${params.BROWSER}.yaml"
                    writeFile file: tempYamlPath, text: jobYaml

                    withCredentials([file(credentialsId: KUBERNETES_CREDENTIALS, variable: 'KUBECONFIG')]) {
                        bat "kubectl apply -f ${CONFIGMAP_PATH} --kubeconfig=%KUBECONFIG%"
                        bat "kubectl apply -f ${tempYamlPath} --kubeconfig=%KUBECONFIG%"
                    }
                }
            }
        }

        stage('Deploy to Kubernetes Cron Job') {
            steps {
                script {
                    withCredentials([file(credentialsId: KUBERNETES_CREDENTIALS, variable: 'KUBECONFIG')]) {
                        bat "kubectl apply -f ${CONFIGMAP_PATH_CRON} --kubeconfig=%KUBECONFIG%"
                        bat "kubectl apply -f ${CRON_JOB_PATH} --kubeconfig=%KUBECONFIG%"
                    }
                }
            }
        }
    } 

    post {
        success {
            echo 'Pipeline completed successfully'
        }
        failure {
            echo 'Pipeline failed'
        }
    }
}
